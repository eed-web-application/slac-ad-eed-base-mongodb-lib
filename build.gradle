plugins {
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'edu.stanford.slac'
version = '0.0.1'
def javaSourceCompatibility = JavaVersion.VERSION_21
def javaTargetCompatibility = JavaVersion.VERSION_21

java {
    sourceCompatibility = javaSourceCompatibility
    targetCompatibility = javaTargetCompatibility

}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/eed-web-application/slac-ad-eed-baselib")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-security',
            'edu.stanford.slac:slac-ad-eed-baselib:0.1.44',
            'org.mapstruct:mapstruct:1.5.3.Final',
            'com.fasterxml.jackson.core:jackson-databind',
            'org.springframework.boot:spring-boot-starter-data-mongodb'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    testImplementation 'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.boot:spring-boot-starter-data-ldap',
            'com.unboundid:unboundid-ldapsdk:6.0.8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "Github"
            url = "https://maven.pkg.github.com/eed-web-application/slac-ad-eed-base-mongodb-lib"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    reportsDirectory = layout.buildDirectory.dir('coverage')
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

testlogger {
    theme 'standard'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showOnlySlow false
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
    logLevel 'lifecycle'
}
